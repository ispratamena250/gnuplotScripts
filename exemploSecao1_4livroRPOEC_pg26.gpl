#Script para visualização do exemplo da seção 1.4 do livro RPOEC na página 26

set xrange [-10:10]
set yrange [-10:10]

set xlabel "x"
set ylabel "y"

set zeroaxis
set samples 100000
set key top left

set title "Exemplo: y^2 = x^3 + 17"

set label 1 "P1 = (-1, 4)" at -1, 4 offset 1, -1
set label 2 "P2 = (2, 5)" at 2, 5 offset 1, -1
set label 3 "P3 = (-8/9, -109/27)" at -0.889, -4.037 offset 1, 1
set label 4 "2P1 = (137/64, -2651/512)" at 2.14, -5.17 offset 1, 1

#Dados inline:
$dados <<EOD
-1	4
2	5
-0.889	-4.037
2.14	-5.17
EOD

f(x) = sqrt(x**3.0 + 17.0) >= 0 ? sqrt(x**3.0 + 17.0) : 1/0
fl(x) = sqrt(x**3.0 + 17.0) >= 0 ? -sqrt(x**3.0 + 17.0) : 1/0
eq1(x) = x/3.0 + 13.0/3.0

plot $dados u 1:2 w p pt 7 ps 2 lc rgb "red" notitle, \
	f(x) w l lw 4 lc rgb "black" title "y^2 = x^3 + 17", \
	fl(x) w l lw 4 lc rgb "black" notitle, \
	eq1(x) w l lw 2 lc rgb "blue" title "y = x/3 + 13/3"
